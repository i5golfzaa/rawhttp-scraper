plugins {
    id 'java'
}

group 'com.athaydes.rawhttp'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

dependencies {
    compile project(':rawhttp-scraper')
    compileOnly 'org.graalvm:graal-sdk:1.0.0-rc5'
}

jar {
    manifest {
        attributes "Main-Class": "com.athaydes.rawhttp.scraper.Main"
    }

    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task nativeImage(type: Exec) {
    dependsOn 'jar'
    inputs.file jar
    outputs.file jar.archivePath.name - '.jar'
    commandLine 'native-image',
            '--language:js',
            '-H:IncludeResources=META-INF/services/.*',
            "-H:ReflectionConfigurationFiles=${project(':rawhttp-scraper').file('graal-reflect-config.json')}," +
                    file('graal-reflect-config.json'),
            '-jar', jar.archivePath
}
