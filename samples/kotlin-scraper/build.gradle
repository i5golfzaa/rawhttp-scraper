plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.60'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'com.athaydes.rawhttp'
version '1.0-SNAPSHOT'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile project(':rawhttp-scraper')
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    testCompile 'io.kotlintest:kotlintest:2.0.7'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes "Main-Class": "com.athaydes.rawhttp.scraper.Main"
    }
}

shadowJar {
    baseName = project.name
    classifier = null
    version = null
    mergeServiceFiles()
}

task nativeImage(type: Exec) {
    dependsOn 'shadowJar'
    inputs.file shadowJar
    outputs.file shadowJar.archivePath.name - '.jar'
    commandLine 'native-image',
            '-H:IncludeResources=META-INF/services/.*',
            "-H:ReflectionConfigurationFiles=${project(':rawhttp-scraper').file('graal-reflect-config.json')}," +
                    file('graal-reflect-config.json'),
            '-jar', shadowJar.archivePath
}
