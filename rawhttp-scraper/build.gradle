plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'com.athaydes.rawhttp'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

dependencies {
    compile 'com.athaydes.rawhttp:rawhttp-core:2.0-beta3'
}

jar {
    manifest {
        attributes "Main-Class": "com.athaydes.rawhttp.scraper.Main"
    }
}

shadowJar {
    baseName = project.name
    classifier = null
    version = null
    mergeServiceFiles()
}

task nativeImage(type: Exec) {
    dependsOn 'shadowJar'
    inputs.file shadowJar
    outputs.file shadowJar.archivePath.name - '.jar'
    commandLine 'native-image',
            '-H:IncludeResources=META-INF/services/.*',
            "-H:ReflectionConfigurationFiles=${file('graal-reflect-config.json')}",
            '-jar', shadowJar.archivePath
}
